
LIBNAME NH 'C:\輔仁大學臨時工\李孟霖醫師\TEST20220319';RUN;
LIBNAME TEMP 'C:\輔仁大學臨時工\李孟霖醫師\TEST20220319\TEMP';RUN;

LIBNAME NHIRD 'D:\NHIRD'; RUN;

DATA ICD_C01; /*下肢靜脈曲張*/
	INPUT ICD $;
	CARDS; 
454
; 
RUN;

DATA ICD_C02; /*二&三尖瓣閉鎖不全*/
	INPUT ICD $;
	CARDS;
424
;
RUN;

DATA ICD_C03; /*主動脈剝離&腹胸主動脈瘤*/
	INPUT ICD $;
	CARDS;
441
;
RUN;


DATA ICD_C04; /*疝氣*/
    INPUT ICD $;
    CARDS;
5550
;
RUN;


DATA ICD_C05; /*痔瘡*/
    INPUT ICD $;
    CARDS;
4550
;
RUN;


DATA ICD_C06; /*高血壓*/
    INPUT ICD $;
    CARDS;
401
402
403
404
405
;
RUN;


DATA ICD_C07; /*糖尿病*/
    INPUT ICD $;
    CARDS;
250
;
RUN;


DATA ICD_C08; /*慢性肺阻塞*/
    INPUT ICD $;
    CARDS;
490
491
492
493
494
495
496
;
RUN;


DATA ICD_C09; /*高脂血症*/
    INPUT ICD $;
    CARDS;
272
;
RUN;


DATA ICD_C10; /*癌症*/
    INPUT ICD $;
    CARDS;
140
141
142
143
144
145
146
147
148
149
150
151
152
153
154
155
156
157
158
159
160
161
162
163
164
165
166
167
168
169
170
171
172
173
174
175
176
177
178
179
180
181
182
183
184
185
186
187
188
189
190
191
192
193
194
195
196
197
198
199
200
201
202
203
204
205
206
207
208
;
RUN;


DATA ICD_C11; /*心房顫動*/
    INPUT ICD $;
    CARDS;
42731
;
RUN;


DATA ICD_C12; /*心臟衰竭*/
    INPUT ICD $;
    CARDS;
428
;
RUN;


DATA ICD_C13; /*缺血性心臟病*/
    INPUT ICD $;
    CARDS;
410
411
412
413
414
;
RUN;


DATA ICD_C14; /*慢性腎臟病*/
    INPUT ICD $;
    CARDS;
403
404
582
585
586
587
588
;
RUN;


DATA ICD_C15; /*中風*/
    INPUT ICD $;
    CARDS;
434
436
430
431
432
;
RUN;


DATA ICD_C16; /*瓣膜相關手術*/
    INPUT ICD $;
    CARDS;
3510
3512
3523
3524
3532
3533
3599
;
RUN;

%LET SS=2000;
%LET EE=2013;


DATA _NULL_;
    DO ICD="C01","C02","C03","C04","C05","C06","C07","C08","C09","C10","C11","C12","C13","C14","C15";
    CALL EXECUTE("PROC SQL;");
    DO YEAR=&SS TO &EE;
        CALL EXECUTE("CREATE TABLE CD_"||ICD||STRIP(YEAR)||" AS
            SELECT DISTINCT 
			A.ID,
			A.ID_BIRTHDAY,
			'CD' AS TYPE,
			A.CASE_TYPE,
			A.FUNC_DATE,
			ACODE_ICD9_1 AS ICD9_1,
			ACODE_ICD9_2 AS ICD9_2,
			ACODE_ICD9_3 AS ICD9_3
            FROM NHIRD.CD"||STRIP(YEAR)||" AS A
            WHERE
                SUBSTR(ACODE_ICD9_1,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_2,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_3,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_1,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_2,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_3,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_1,1,5) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_2,1,5) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ACODE_ICD9_3,1,5) IN (SELECT ICD FROM ICD_"||ICD||")
                ;");
        CALL EXECUTE("CREATE TABLE DD_"||ICD||STRIP(YEAR)||" AS
            SELECT 
			A.ID,
			A.ID_BIRTHDAY,
			'DD' AS TYPE, 
			A.CASE_TYPE,
			A.IN_DATE AS FUNC_DATE,
			ICD9CM_CODE AS ICD9_1,
			ICD9CM_CODE_1 AS ICD9_2,
			ICD9CM_CODE_2 AS ICD9_3,
			ICD9CM_CODE_3 AS ICD9_4,
			ICD9CM_CODE_4 AS ICD9_5
            FROM NHIRD.DD"||STRIP(YEAR)||" AS A
            WHERE
                SUBSTR(ICD9CM_CODE,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_1,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_2,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_3,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_4,1,3) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_1,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_2,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_3,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_4,1,4) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE,1,5) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_1,1,5) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_2,1,5) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_3,1,5) IN (SELECT ICD FROM ICD_"||ICD||") OR
                SUBSTR(ICD9CM_CODE_4,1,5) IN (SELECT ICD FROM ICD_"||ICD||")
                ;");
    END;
    CALL EXECUTE("QUIT;");
    END;
RUN;

DATA _NULL_;  /*C01~15 裡面有CD&DD 2000~2013的所有資料*/
    DO ICD="C01","C02","C03","C04","C05","C06","C07","C08","C09","C10","C11","C12","C13","C14","C15";
    CALL EXECUTE("DATA NH."||ICD||";");
    CALL EXECUTE("SET CD_"||ICD||"2000-CD_"||ICD||"2013 DD_"||ICD||"2000-DD_"||ICD||"2013;");
    CALL EXECUTE("RUN;");
    END;
RUN;

/*先取出下肢靜脈曲張代碼者  C01*/
/*分成下肢肢靜脈曲張組(CASE) 非下肢靜脈曲張組(CONTROL) */
/*CD 要3次 & DD要1次  */
/*用flag來做*/
/*取門診3次與住院1次的交集*/

/*取曾經住院過1次的病患id*/
PROC SQL; 
CREATE TABLE DD_TEMP_CASE AS 
SELECT DISTINCT 
	A.ID,
	CASE WHEN A.TYPE IN ('DD') THEN 1 ELSE 0 END AS FLAG
FROM NH.C01 AS A
;
QUIT;
PROC SQL; 
CREATE TABLE DD_CASE AS 
SELECT DISTINCT 
	A.ID
FROM DD_TEMP_CASE AS A
WHERE FLAG = 1;
QUIT;
/*取曾經門診過3次的病患id*/
PROC SQL; 
CREATE TABLE CD_TEMP_CASE AS 
SELECT DISTINCT 
	A.ID,
	A.TYPE,
	COUNT(DISTINCT FUNC_DATE) AS COUNT
FROM NH.C01 AS A
GROUP BY ID,TYPE;
QUIT;
PROC SQL;
CREATE TABLE CD_CASE AS 
SELECT DISTINCT
	A.ID
FROM CD_TEMP_CASE AS A
WHERE 
	A.TYPE IN ('CD') AND
	A.COUNT > 2
;
QUIT;
/*取交集為CASE的ID*/
PROC SQL;
CREATE TABLE CASE_ID  AS
SELECT DISTINCT
	A.ID
FROM CD_CASE AS A
INNER JOIN DD_CASE AS B 
ON A.ID = B.ID;
QUIT;

/*取CASE跟CONTROL的所有資訊*/
PROC SQL;
CREATE TABLE NH.CASE AS
SELECT DISTINCT
	A.*
FROM NH.C01 AS A
INNER JOIN CASE_ID AS B 
ON A.ID = B.ID;
QUIT;

PROC SQL;
CREATE TABLE NH.CONTROL AS
SELECT DISTINCT
	A.*
FROM NH.C01 AS A
LEFT OUTER JOIN CASE_ID AS B ON(A.ID = B.ID)
WHERE B.ID IS NULL;
;
QUIT;
/*排除 1.研究對象年齡小於等於20歲*/
/*計算AGE並排除*/
PROC SQL;
CREATE TABLE NH.CASE_EXCLUDE1 AS
SELECT DISTINCT
	A.*
FROM NH.CASE AS A
WHERE 
	FLOOR(
		YRDIF(
			INPUT(CATS(A.ID_BIRTHDAY,'15'),YYMMDD8.),
			INPUT(A.FUNC_DATE,YYMMDD8.),
		'ACT/ACT')
		) > 20
;
QUIT;
/*排除 2.性別不明*/
/*乾 排除不了C01裡面沒有性別*/
/*

PROC SQL;
CREATE TABLE NH.CASE_EXCLUDE2 AS
SELECT DISTINCT
	A.*
FROM NH.CASE_EXCLUDE1 AS A
WHERE 
	ID_SEX='F' OR
	ID_SEX='M'
QUIT;

*/


/*排除 3.曾經確診任何瓣膜性心臟病*/

PROC SQL;  
CREATE TABLE  NH.CASE_EXCLUDE3 AS  
SELECT DISTINCT
	A.*
FROM NH.CASE_EXCLUDE1 AS A /*應該是NH.CASE_EXCLUDE2 但上面性別沒有排除*/
LEFT OUTER JOIN NH.C02 AS B ON(A.ID = B.ID)
WHERE B.ID IS NULL;
;
QUIT;

/*排除 4.主動脈剝離5.腹主動脈瘤(同一個ICD中)*/
PROC SQL;  
CREATE TABLE  NH.CASE_EXCLUDE4 AS  
SELECT DISTINCT
	A.*
FROM NH.CASE_EXCLUDE3 AS A 
LEFT OUTER JOIN NH.C03 AS B ON(A.ID = B.ID)
WHERE B.ID IS NULL;
;
QUIT;

/*排除 6.	疝氣與痔瘡者*/
PROC SQL;  /*疝氣*/
CREATE TABLE  NH.CASE_EXCLUDE5 AS  
SELECT DISTINCT
	A.*
FROM NH.CASE_EXCLUDE4 AS A 
LEFT OUTER JOIN NH.C04 AS B ON(A.ID = B.ID)
WHERE B.ID IS NULL;
;
QUIT;
PROC SQL;  /*痔瘡*/
CREATE TABLE  NH.CASE_EXCLUDE6 AS  
SELECT DISTINCT
	A.*
FROM NH.CASE_EXCLUDE5 AS A 
LEFT OUTER JOIN NH.C05 AS B ON(A.ID = B.ID)
WHERE B.ID IS NULL;
;
QUIT;
/*NH.CASE_EXCLUDE6 為人次 還未歸人 */
